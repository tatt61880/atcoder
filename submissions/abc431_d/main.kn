func main()
	var n: int :: cui@inputInt()
	var w: []int :: #[n]int
	var h: []int :: #[n]int
	var b: []int :: #[n]int
	var totalW: int :: 0
	for i(0, n - 1)
		do w[i] :: cui@inputInt()
		do h[i] :: cui@inputInt()
		do b[i] :: cui@inputInt()
		do totalW :+ w[i]
	end for
	
	; dp[hw]: 頭の重さがちょうどhwのときの最大幸福値
	var maxHW: int :: totalW / 2
	var dp: []int :: #[maxHW + 1]int
	for i(0, n - 1)
		for hw(maxHW, 0, -1)
			if(hw - w[i] >= 0)
				do dp[hw] :: lib@max(dp[hw] + b[i], dp[hw - w[i]] + h[i])
			else
				do dp[hw] :+ b[i]
			end if
		end for
	end for
	
	var ans: int :: 0
	for hw(0, maxHW)
		do ans :: lib@max(ans, dp[hw])
	end for
	do cui@print("\{ans}\n")
end func
