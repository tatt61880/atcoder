func main()
	var t: int :: cui@inputInt()
	for(1, t)
		var a: []char :: cui@input()
		var b: []char :: cui@input()
		
		var baa: []char :: b ~ a ~ a
		var z: []int :: @zAlgorithm(baa)
		
		var ans: int :: -1
		var n: int :: ^a
		for i(n, 2 * n - 1)
			if(z[i] >= n)
				do ans :: i - n
				break i
			end if
		end for
		do cui@print("\{ans}\n")
	end for
end func

func zAlgorithm(s: []char): []int
	var n: int :: ^s
	var z: []int :: #[n]int
	do z[0] :: n
	var l: int :: 0
	var r: int :: 0
	for i(1, n - 1)
		var zi: int :: 0
		if(i <= r)
			do zi :: lib@min(r - i + 1, z[i - l])
		end if
		while(i + zi < n & s[zi] = s[i + zi])
			do zi :+ 1
		end while
		do z[i] :: zi
		if(i + zi - 1 > r)
			do l :: i
			do r :: i + zi - 1
		end if
	end for
	ret z
end func
