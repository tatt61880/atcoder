func main()
	var n: int :: cui@inputInt()
	
	var s: [][]char :: #[n][]char
	var t: [][]char :: #[n][]char
	
	for i(0, n - 1)
		do s[i] :: cui@input()
	end for
	for i(0, n - 1)
		do t[i] :: cui@input()
	end for
	
	var ans: int :: n * n
	
	for i(0, 3)
		do ans :: lib@min(ans, diff(s, t) + i)
		do s :: rotate(s)
	end for
	
	do cui@print("\{ans}\n")
	
	func diff(s: [][]char, t: [][]char): int
		var res: int :: 0
		var n: int :: ^s
		for i(0, n - 1)
			for j(0, n - 1)
				do res :+ s[i][j] <> t[i][j] ?(1, 0)
			end for
		end for
		ret res
	end func
	
	func rotate(s: [][]char): [][]char
		var n: int :: ^s
		var res: [][]char :: #[n, n]char
		for i(0, n - 1)
			for j(0, n - 1)
				do res[j][n - 1 - i] :: s[i][j]
			end for
		end for
		ret res
	end func
end func
